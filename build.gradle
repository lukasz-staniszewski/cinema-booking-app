plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "com.moowork.node" version "1.3.1"
}

group = 'com.thai-chicken'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

node {
    version = '16.13.0'
    download = false

    // Set the work directory for unpacking node
    workDir = file("${project.projectDir}/src/main-front/")

    // Set the work directory for NPM
    npmWorkDir = file("${project.projectDir}/src/main-front/npm")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/src/main-front/")

}

task npmInstallDependencies(type: NpmTask) {
    description = "Installs all dependencies from package.json"
    dependsOn 'npmSetup'
    args = ["install"]
}

task npmBuild(type: NpmTask) {
    description = "Builds production version of the webapp"
    dependsOn 'npmInstallDependencies'
    args = ["run", "build"]
}

task copyFrontToBuild(type: Copy) {
    dependsOn 'npmBuild'
    from "$projectDir/src/main-front/build/"
    into "$buildDir/resources/main/static/."
}

task uiTest(type: NpmTask) {
    environment = ['CI': 'true']
    args = ['run', 'test']
}

task uiRun(type: NpmTask) {
    dependsOn("npmBuild")
    dependsOn("uiTest")
    args = ['start']
}

processResources {
    dependsOn 'copyFrontToBuild'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.6'
}

test {
    useJUnitPlatform()
}
